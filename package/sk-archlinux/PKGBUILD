# Maintainer: Skirmantas Kligys <Skirmantas.Kligys@gmail.com>

pkgname=waybar-git-sk
pkgver=r948.e9b0365
pkgrel=1
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors (GIT), with SK patches'
arch=('x86_64')
url="https://github.com/Alexays/Waybar/"
license=('MIT')
provides=('waybar')
conflicts=('waybar')
depends=(
    'gtkmm3'
    'libjsoncpp.so'
    'libsigc++'
    'fmt'
    'jack' 'libjack.so'
    'wayland'
    'libdate-tz.so'
    'libspdlog.so'
    'gtk-layer-shell'
    'libupower-glib.so'
    'upower'
    'libevdev'
    'libinput'
    'libpulse'
    'spdlog'
    'libnl'
    'libappindicator-gtk3'
    'libdbusmenu-gtk3'
    'libmpdclient'
    'libsndio.so'
    'libxkbcommon'
    'libwireplumber'
    'playerctl'
    # SK added:
    'gammastep'
)
makedepends=(
    'cmake'
    'catch2'
    'meson'
    'scdoc' # For generating manpages
    'wayland-protocols'
)
backup=(
    etc/xdg/waybar/config
    etc/xdg/waybar/style.css
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)
source=(
    "${pkgname}::git+https://github.com/Alexays/Waybar#commit=41de8964f1e3278edf07902ad68ca5e01e7abeeb"
    'gammastep.patch'
)
sha256sums=(
    '0a492b34a51ccf5ec1afc3b561648f49111775ff606383aacba17a10da0baf84'
    'a65c4792d29722416dad38f9124c58474c179a211868b33b42d93cbf3c7a1005'
)

pkgver() {
    cd "${srcdir}/${pkgname}"
    git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/${pkgname}"
    patch --forward --strip=1 --input="${srcdir}/gammastep.patch"
}

build() {
    cd "${srcdir}/${pkgname}"
    rm -rf "${srcdir}/build"
    meson --prefix=/usr "${srcdir}/build"
    ninja -C "${srcdir}/build"
}

package() {
    cd "${srcdir}/${pkgname}"
    DESTDIR="$pkgdir" ninja -C "${srcdir}/build" install
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
